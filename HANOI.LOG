TO MOVE :FROM :TO
ON :TO OFF :FROM
END

TO TRANSPOSE :N :FROM :TO
IF :N = 1 [MOVE :FROM :TO STOP]
LOCAL "TEMP
MAKE "TEMP WORD "L 6 - (LAST :FROM) - (LAST :TO)
TRANSPOSE (:N - 1) :FROM :TEMP
MOVE :FROM :TO
TRANSPOSE (:N - 1) :TEMP :TO
END

TO TOWER :NUMBER :PLACE
HT CS PU BK 100 RT 90 BK 160 PD
TT [Towers of Hanoi from ST LOGO User's Guide]
MAKE "L1 [] MAKE "L2 [] MAKE "L3 []
MAKE "X [-200 0 200] ; LOCATION OF THE THREE PLACES
MAKE "Y HEIGHTS :NUMBER
SETFILL [1 1 1]
MAKE "GFILL "TRUE ; ENABLE FILL
BUILD :NUMBER 10 "L1
TRANSPOSE :NUMBER "L1 WORD "L :PLACE
END

TO PLACE :N :S :P
IF :N < 1 [STOP]
BOX (SE (ITEM LAST :P :X) - :S / 2 (ITEM :N :Y) :S 8)
END

TO ON :P :S
MAKE :P SE THING :P :S
SETFILL [1 1 1]
PLACE COUNT THING :P :S :P
END

TO OFF :P
LOCAL "L MAKE "L THING :P
LOCAL "S MAKE "S LAST :L
SETFILL [1 1 0]
PLACE COUNT :L :S :P
MAKE :P BUTLAST :L
OP :S
END

TO BUILD :N :S :P
IF :N = 0 [STOP]
PLACE :N :S :P
MAKE :P SE :S THING :P
BUILD :N - 1 :S + 10 :P
END

TO HEIGHTS :N
IF :N = 1 [OP -60]
OP SE HEIGHTS :N - 1 (11 * :N - 71)
END

TO DEMO
SETZOOM (ROUND (ITEM 3 SYSFACTS)/80)/8
TOWER :N 3
MAKE "N :N + 1
DEMO
END

MAKE "N 2
FS SETBG 0 SETPC 1 DEMO
