TO INIT
SETZOOM 1 SETSCRUNCH 1

MAKE "MAXCOL -1 + ITEM 5 SYSFACTS
TEST :MAXCOL = 0
IFT [DEFINE "SETCOL [[C][SETPC REMAINDER :C 2]]]
IFF [DEFINE "SETCOL [[C][SETPC 2 + REMAINDER :C + 1 :MAXCOL]]]

CS PU SETH 90
SETPAN LIST (ITEM 3 SYSFACTS) * .5 (ITEM 4 SYSFACTS) * .5
END

TO MANDEL.PLOT
(LOCAL "X "Y "X2 "Y2 "I)
MAKE "X 0
MAKE "Y 0
MAKE "X2 0
MAKE "Y2 0

MAKE "X0 :X0 + :SX

MAKE "I 0
LABEL "ILOOP
  MAKE "Y (:X + :X) * :Y + :Y0
  MAKE "X :X2 - :Y2 + :X0
  MAKE "X2 :X * :X
  MAKE "Y2 :Y * :Y
  IF :X2 + :Y2 > 4 [SETCOL :I GO "OUT]
MAKE "I :I + 1
IF :I < 16 [GO "ILOOP]

SETPC 1
LABEL "OUT
PD FD 1 PU
END

TO MANDEL.COL
LOCAL "X0
MAKE "X0 :CX0
MAKE "Y0 :Y0 + :SY
SETX 0
REPEAT :W [MANDEL.PLOT]
MAKE "PY :PY + 1
SETY :PY
END

TO MANDEL :W :H :CX0 :CY0 :CX1 :CY1
(LOCAL "PY "SX "SY "Y0)
MAKE "SX (:CX1 - :CX0) / :W
MAKE "SY (:CY1 - :CY0) / :H
MAKE "Y0 :CY0 - (:SY * .5)
MAKE "PY 0
REPEAT :H [MANDEL.COL]
END

TO DEMO
FS INIT
(LOCAL "W "H)
MAKE "W ITEM 3 SYSFACTS
MAKE "H ITEM 4 SYSFACTS
MANDEL :W :H -2.05 -1 .6 1
END

DEMO
